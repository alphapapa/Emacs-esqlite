;;; sqlite3-helm.el --- Define helm source for sqlite3 database

;; Author: Masahiro Hayashi <mhayashi1120@gmail.com>
;; Keywords: data
;; URL: https://github.com/mhayashi1120/sqlite3.el/raw/master/sqlite3-helm.el
;; Emacs: GNU Emacs 24 or later
;; Package-Requires: ((sqlite3 "0.1.0") (helm "20131207.845"))
;; Version: 0.1.0

;; This program is free software; you can redistribute it and/or
;; modify it under the terms of the GNU General Public License as
;; published by the Free Software Foundation; either version 3, or (at
;; your option) any later version.

;; This program is distributed in the hope that it will be useful, but
;; WITHOUT ANY WARRANTY; without even the implied warranty of
;; MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU
;; General Public License for more details.

;; You should have received a copy of the GNU General Public License
;; along with GNU Emacs; see the file COPYING.  If not, write to the
;; Free Software Foundation, Inc., 51 Franklin Street, Fifth Floor,
;; Boston, MA 02110-1301, USA.

;;; Commentary:

;;; TODO:
;; * GLOB syntax is greater than LIKE syntax
;;   http://pokutuna.hatenablog.com/entry/20111113/1321126659

;; * add samples

;;; Code:

(eval-when-compile
  (require 'cl))

(require 'sqlite3)
(require 'pcsv)

;; do not require helm

(declare-function 'helm-log "helm")
(declare-function 'helm-get-current-source "helm")

(defvar helm-pattern)
(defvar helm-async-processes)

;;;
;;; Sqlite3 for helm
;;;

(defvar sqlite3-helm-history nil)

(defface sqlite3-helm-finish
  '((t (:foreground "Green")))
  "Face used in mode line when sqlite is finish."
  :group 'helm-grep)

;;;###autoload
(defun sqlite3-helm-define (source)
  "This function provides extension while `helm' source composing.
Normally, should not override `candidates-process', `candidates',
 `candidate-transformer' directive.
You must choose `sqlite3-composer' or `sqlite3-table' directive.

Following sqlite3 specific directive:
`sqlite3-db' File name of sqlite3 database or sqlite3 stream. 
`sqlite3-composer' Function which accept one argument `helm-pattern' and return
    a sql query string.
`sqlite3-async' Indicate SOURCE is async.

`sqlite3-table' Table name of result set. This directive only meaningful
     `sqlite3-composer' is not specified.
  `sqlite3-column' Column name of you desired to search.
  `sqlite3-display-column' Column name of select.

Following default directive:
`real-to-display' directive accept a list of string, generated by SELECT statement
 from `sqlite3-composer'. Default behavior is displaying a first column of SELECT.
`delayed', `volatile'

Other helm directive is overridable.

If SOURCE don't contain `sqlite3-async', you should add LIMIT statement
 to SQL which composed by `sqlite3-composer'.
 See the syntax of LIMIT statement.
http://www.sqlite.org/lang_select.html"
  (let ((file-or-stream (assoc-default 'sqlite3-db source))
        (composer (assoc-default 'sqlite3-composer source))
        (async (assoc 'sqlite3-async source))
        file stream)
    (cond
     ((stringp file-or-stream)
      (setq file file-or-stream))
     ((sqlite3-stream-p file-or-stream)
      (setq stream file-or-stream))
     (t
      (error "sqlite3-db: Not a valid filename or stream %s" file)))
    (cond
     ((and composer (not (functionp composer)))
      (error "sqlite3-composer: Not a valid function"))
     ((and (null composer))
      (setq composer (sqlite3-helm--construct-composer
                      file-or-stream source))))
    (let ((result
           ;; dequote all cdr to `cons' the list
           `((name . ,"sqlite3")
             (real-to-display . ,'sqlite3-helm-make-one-line)
             ,@(cond
                (stream
                 `((candidates
                    .
                    (lambda ()
                      (sqlite3-helm-call-stream
                       ,stream
                       (funcall ',composer helm-pattern))))
                   ;; suppress caching
                   (volatile)))
                (async
                 `((candidates-process
                    .
                    (lambda ()
                      (sqlite3-helm-start-command
                       ,file
                       (funcall ',composer helm-pattern))))
                   (candidate-transformer . sqlite3-helm-hack-for-multiline)))
                (t
                 `((candidates
                    .
                    (lambda ()
                      (sqlite3-helm-call-command
                       ,file
                       (funcall ',composer helm-pattern))))
                   ;; suppress caching
                   (volatile))))
             (match . ,'identity)
             (candidate-number-limit . ,helm-candidate-number-limit)
             (history . ,'sqlite3-helm-history))))
      (dolist (s source)
        (let ((cell (assq (car-safe s) result)))
          (cond
           (cell
            (setcdr cell (cdr s)))
           (t
            (setq result (cons s result))))))
      result)))

(defun sqlite3-helm--construct-composer (file-or-stream source)
  (let* ((table (assoc-default 'sqlite3-table source))
         (column (assoc-default 'sqlite3-column source))
         (dispcolumn (assoc-default 'sqlite3-display-column source))
         (columns (cond
                   ((sqlite3-stream-p file-or-stream)
                    (sqlite3-read-table-columns file-or-stream table))
                   ((stringp file-or-stream)
                    (sqlite3-file-table-columns file-or-stream table))
                   (t (error "Assert"))))
         (limit (or (assoc-default 'candidate-number-limit source) 100)))
    (unless table
      (error "sqlite3-table are required"))
    (let ((dispcolumns (if dispcolumn (list dispcolumn) columns)))
      `(lambda ()
         (let ((dispcolumns ',dispcolumns)
               (table ,table)
               (limit ,limit)
               (search-columns ',(if column (list column) columns))
               (likepat (sqlite3-helm-glob-to-fuzzy-like helm-pattern)))
           (sqlite3-format
            '("SELECT %O{dispcolumns}"
              " FROM  %o{table}"
              " WHERE %s"
              " LIMIT %s{limit}")
            (mapconcat
             (lambda (col)
               (sqlite3-format "%o{col} LIKE %%T{likepat} ESCAPE '\\'"))
             search-columns
             " OR ")))))))

(defun sqlite3-helm-match-function (cand)
  (string-match (sqlite3-helm-glob-to-regexp helm-pattern) cand))

(defun sqlite3-helm-call-stream (stream query)
  (condition-case err
      (mapcar
       'sqlite3-helm--construct-row
       (sqlite3-stream-read stream query))
    (error
     (helm-log "Error: sqlite3 %s"
               (replace-regexp-in-string
                "\n" ""
                (prin1-to-string (cdr err)))))))

(defun sqlite3-helm-call-command (file query)
  (condition-case err
      (mapcar
       'sqlite3-helm--construct-row
       (with-temp-buffer
         (unless (zerop (sqlite3-call-csv-process file query ""))
           (error "Process exited abnormally %s" (buffer-string)))
         (goto-char (point-min))
         (pcsv-parse-buffer)))
    (error
     (helm-log "Error: sqlite3 %s"
               (replace-regexp-in-string
                "\n" ""
                (prin1-to-string (cdr err)))))))

(defun sqlite3-helm-start-command (file query)
  "Initialize async locate process for `helm-source-locate'."
  ;; sqlite3-helm implementation ignore NULL. (NULL is same as a empty string)
  (let ((proc (sqlite3-start-csv-process file query "")))
    (set-process-sentinel proc 'sqlite3-helm--process-sentinel)
    proc))

(defun sqlite3-helm--process-sentinel (proc event)
  (when (memq (process-status proc) '(exit signal))
    (let ((buf (process-buffer proc)))
      (when (buffer-live-p buf)
        (kill-buffer buf)))
    (let ((source (cdr (assq proc helm-async-processes))))
      (unless (zerop (process-exit-status proc))
        (helm-log "Error: sqlite3 %s"
                  (replace-regexp-in-string "\n" "" event))))))

(defun sqlite3-helm-hack-for-multiline (candidates)
  ;; helm split csv stream by newline. restore the csv as one text
  ;; and try to parse it.

  ;; No newline in one value. (No problem)
  ;; OUTPUT-STRING: a,b\nc,d\ne CANDIDATES: ("a,b" "c,d") INCOMPLETE-LINE: "e"
  ;; OUTPUT-STRING: a,b\nc,d\n  CANDIDATES: ("a,b" "c,d") INCOMPLETE-LINE: ""
  ;; OUTPUT-STRING: a,b\nc,d    CANDIDATES: ("a,b")       INCOMPLETE-LINE: "c,d"

  ;; There is newline and quote by double-quote.
  ;; OUTPUT-STRING: a,b\nc,"d\nD"\ne CANDIDATES: ("a,b" "c,\"d\nD\"") INCOMPLETE-LINE: "e"
  ;; OUTPUT-STRING: a,b\nc,"d\nD"\n  CANDIDATES: ("a,b" "c,\"d\nD\"") INCOMPLETE-LINE: ""
  ;; OUTPUT-STRING: a,b\nc,"d\nD"    CANDIDATES: ("a,b" "c,\"d")      INCOMPLETE-LINE: "D\""
  ;; OUTPUT-STRING: a,b\nc,"d\nD     CANDIDATES: ("a,b" "c,\"d")      INCOMPLETE-LINE: "D"
  ;; OUTPUT-STRING: a,b\nc,"d\n      CANDIDATES: ("a,b" "c,\"d")      INCOMPLETE-LINE: ""
  ;; OUTPUT-STRING: a,b\nc,"d        CANDIDATES: ("a,b")              INCOMPLETE-LINE: "c,\"d"
  (let* ((rawtext (concat (mapconcat 'identity candidates "\n") "\n"))
         (source (helm-get-current-source))
         (incomplete-info (assq 'incomplete-line source)))
    (destructuring-bind (data rest) (sqlite3-helm--read-csv rawtext)
      (setcdr incomplete-info (concat rest (cdr incomplete-info)))
      data)))

;; try to read STRING until end.
;; csv line may not terminated and may contain newline.
(defun sqlite3-helm--read-csv (string)
  (with-temp-buffer
    (insert string)
    (goto-char (point-min))
    (let ((res '())
          (start (point)))
      (condition-case nil
          (while (not (eobp))
            (let ((ln (sqlite3--read-csv-line)))
              (setq start (point))
              (setq res (cons (sqlite3-helm--construct-row ln) res))))
        (invalid-read-syntax))
      (list (nreverse res)
            (buffer-substring-no-properties start (point-max))))))

(defun sqlite3-helm--construct-row (csv-line)
  ;; TODO investigate it! helm seems ignore first item of list. what is it?
  (cons 'dummy csv-line))


;;;
;;; Any utilities
;;;

(defun sqlite3-helm-make-one-line (row &optional width)
  "To display TEXT as a helm line."
  (let* ((text (mapconcat (lambda (x)
                            (cond
                             ((stringp x) x)
                             ((eq x :null) "")
                             (t "")))
                          row " "))
         (oneline (replace-regexp-in-string "\n" " " text)))
    (truncate-string-to-width oneline (or width (window-width)))))

;;TODO regexp-quote
(defun sqlite3-helm-glob-to-regexp (glob &optional escape-char)
  (sqlite3--replace
   glob
   '((?* . ".*")                        ;TODO greedy?
     (?? . ".?")
     (?\\ (?* . "*") (?\? . "?") (?\\ . "\\")))))

(defun sqlite3-helm-split-fuzzy-glob (glob &optional escape-char)
  (let (prefix suffix)
    (cond
     ((string-match "\\`\\^" glob)
      (setq glob (substring glob 1)))
     ((string-match "\\`\\\\\\^" glob)
      ;; escaped ^.
      (setq glob (substring glob 1)
            prefix t))
     (t
      (setq prefix t)))
    (cond
     ((and (eq escape-char ?\\)
           (string-match "\\`\\(?:\\\\.\\|[^\\\\]\\)*\\\\$\\'" glob))
      ;; end with escaped `$'.
      ;; consider "\\$" <- escaped backslash end with `$'
      (setq glob (concat (substring glob 0 -2) "$"))
      (setq suffix t))
     ((and (not (eq escape-char ?\\))
           ;; end with escaped `$' "\\$"
           (string-match "\\\\\\$\\'" glob))
      (setq glob (concat (substring glob 0 -2) "$"))
      (setq suffix t))
     ((string-match "\\$\\'" glob)
      ;; end of non-escaped `$'
      (setq glob (substring glob 0 -1)))
     (t
      (setq suffix t)))
    (list prefix glob suffix)))

;;;###autoload
(defun sqlite3-helm-glob-to-like (glob &optional escape-char)
  "Convenient function to provide unix like GLOB convert to sql like pattern.

`*' Like glob, match to text more equal zero.
`?' Like glob, match to a char in text.

Above syntax can escape by \\ (backslash). But no relation to ESCAPE-CHAR.
See related information at `sqlite3-escape-like'.

e.g. hoge*foo -> hoge%foo
     hoge?foo -> hoge_foo"
  (sqlite3--replace
   glob
   (sqlite3-escape--like-table
    escape-char
    `((?* . "%")
      (?\? . "_")
      (?\\ (?\* . "*")
           (?\? . "?")
           ,@(if (or (eq ?\\ escape-char) (null escape-char))
                 '((?\\ . "\\\\"))
               '((?\\ . "\\"))))))))

;;;###autoload
(defun sqlite3-helm-glob-to-fuzzy-like (glob &optional escape-char)
  "Convert pseudo GLOB to like syntax to support helm behavior.

Following extended syntax:

`^': Like regexp, match to start of text.
`$': Like regexp, match to end of text.

Above syntax can escape by \\ (backslash). But no relation to ESCAPE-CHAR.
See related information at `sqlite3-escape-like'.

ESCAPE-CHAR pass to `sqlite3-helm-glob-to-like'"
  (destructuring-bind (prefix pattern suffix)
      (sqlite3-helm-split-fuzzy-glob glob (or escape-char ?\\))
    (concat
     (and prefix "%")
     (sqlite3-helm-glob-to-like pattern escape-char)
     (and suffix "%"))))

;;;###autoload
(defun sqlite3-helm-glob-to-fuzzy-glob (glob)
  "Convert GLOB to fuzzy glob to support helm behavior

There are extended syntax `^' `$'. See `sqlite3-helm-glob-to-fuzzy-like'."
  (destructuring-bind (prefix pattern suffix)
      (sqlite3-helm-split-fuzzy-glob glob)
    (concat
     (and prefix "*")
     pattern
     (and suffix "*"))))

(provide 'sqlite3-helm)

;;; sqlite3-helm.el ends here
